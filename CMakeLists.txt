CMAKE_MINIMUM_REQUIRED(VERSION 3.0.0)
PROJECT(RedisAI)

IF (NOT DEPS_PATH)
    # dependencies are required!
    MESSAGE(FATAL_ERROR "DEPS PATH Missing!")
ENDIF()

GET_FILENAME_COMPONENT(depsAbs
    "${DEPS_PATH}" REALPATH
    BASE_DIR "${CMAKE_BINARY_DIR}")

LIST(APPEND CMAKE_PREFIX_PATH ${depsAbs})
INCLUDE_DIRECTORIES(${depsAbs}/include)

SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -fPIC")
SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -fPIC")
SET(CMAKE_C_STANDARD 99)

FIND_LIBRARY(TF_LIBRARIES NAMES tensorflow libtensorflow.so)
IF (NOT TF_LIBRARIES)
    MESSAGE(FATAL_ERROR "Could not find tensorflow")
ENDIF()

FIND_LIBRARY(ORT_LIBRARIES NAMES onnxruntime)
IF (NOT ORT_LIBRARIES)
    MESSAGE(FATAL_ERROR "Could not find ONNXRuntime")
ENDIF()

INCLUDE_DIRECTORIES(${depsAbs}/libtensorflow/include)

IF (APPLE)
    FIND_LIBRARY(MKL_LIBRARIES NAMES mklml)
    IF (NOT MKL_LIBRARIES)
        MESSAGE(FATAL_ERROR "Could not find MKL for Mac")
    ENDIF()
    SET(platDeps "${MKL_LIBRARIES}")
ENDIF()

# Find Torch stuff and build our wrapper
FIND_PACKAGE(Torch REQUIRED)
INCLUDE_DIRECTORIES(util/libtorch_c)
ADD_DEFINITIONS(-DREDISMODULE_EXPERIMENTAL_API)

ADD_SUBDIRECTORY(util/libtorch_c)
ADD_SUBDIRECTORY(src)
ADD_LIBRARY(redisai SHARED $<TARGET_OBJECTS:redisai_obj>)

SET_TARGET_PROPERTIES(redisai PROPERTIES PREFIX "")
SET_TARGET_PROPERTIES(redisai PROPERTIES SUFFIX ".so")

IF (APPLE)
    SET_TARGET_PROPERTIES(redisai PROPERTIES
        LINK_FLAGS "-undefined dynamic_lookup")
ENDIF()

ADD_LIBRARY(redisai_tensorflow SHARED $<TARGET_OBJECTS:redisai_tensorflow_obj>)
TARGET_LINK_LIBRARIES(redisai_tensorflow ${TF_LIBRARIES})
SET_TARGET_PROPERTIES(redisai_tensorflow PROPERTIES PREFIX "")
SET_TARGET_PROPERTIES(redisai_tensorflow PROPERTIES SUFFIX ".so")

ADD_LIBRARY(redisai_torch SHARED $<TARGET_OBJECTS:redisai_torch_obj>)
TARGET_LINK_LIBRARIES(redisai_torch torch_c ${platDeps})
SET_TARGET_PROPERTIES(redisai_torch PROPERTIES PREFIX "")
SET_TARGET_PROPERTIES(redisai_torch PROPERTIES SUFFIX ".so")

ADD_LIBRARY(redisai_onnxruntime SHARED $<TARGET_OBJECTS:redisai_onnxruntime_obj>)
TARGET_LINK_LIBRARIES(redisai_onnxruntime ${ORT_LIBRARIES})
SET_TARGET_PROPERTIES(redisai_onnxruntime PROPERTIES PREFIX "")
SET_TARGET_PROPERTIES(redisai_onnxruntime PROPERTIES SUFFIX ".so")
